shell.executable("/bin/bash")
shell.prefix("source ~/.bash_profile; ")

""" Snakefile for running 10x pipeline """
# cellranger directory added to .bashrc 

configfile: "config.yaml"

DATA = config["DATA"]
RUN = config["RUN"]
TRANSCRIPTOME = config["TRANSCRIPTOME"]
SAMPLE_SHEET = config["SAMPLE_SHEET"]
PROJECT = config["PROJECT"]
LANES = config["LANES"]
SAMPLES = config["SAMPLES"]
INDICIES = config["INDICIES"]

rule all:
    input:
      expand("{data}/raw_data/Stats/DemultiplexingStats.xml",
      data=DATA),
      expand("{sample}_complete.txt", sample = SAMPLES)

rule cellranger_count:
    """ note that cellranger count will fail if the output directory
    specified with ``--id`` exists prior to running. By default Snakemake 
    will generate directories listed in the input and output params, so to
    avoid this error this rule creates a dummy file "{sample}_complete.txt """
    input:
      "{data}/raw_data/Stats/DemultiplexingStats.xml".format(data=DATA)      
    output:
      "{sample}_complete.txt"
    params:
      indir = "{data}/raw_data/{project}".format(data=DATA, project = PROJECT),
      lanes = LANES,
      job_name = "count",
      memory = "select[mem>4] rusage[mem=4]",
    log: "logs/{sample}_count.out"
    threads: 2
    shell:
      """
      set -x
      cellranger count \
          --id={wildcards.sample} \
          --fastqs={params.indir} \
          --sample={wildcards.sample} \
          --lanes={params.lanes} \
          --jobmode=lsf \
          --project={PROJECT} \
          --maxjobs=24 \
          --transcriptome={TRANSCRIPTOME}
      echo "finished" > {output}
      """ 

rule cellranger_mkfastq:
    input:
      SAMPLE_SHEET 
    output:
      "{data}/raw_data/Stats/DemultiplexingStats.xml"      
    params:
      outdir = "{data}/raw_data/",
      job_name = "count",
      memory = "select[mem>4] rusage[mem=4]",
    log: "{data}/results/logs/count.out"
    threads: 2
    shell:
      """
      module load bcl2fastq
      cellranger mkfastq \
          --run={RUN} \
          --samplesheet={SAMPLE_SHEET} \
          --project={PROJECT} \
          --jobmode=lsf \
          --maxjobs=24 \
          --output-dir {params.outdir} \
          --lanes {LANES} 
      module unload bcl2fastq
      """

